import QtQuick

Canvas {
    id: root
    property color color: "#685496"
    property var roundedPolygon: null
    property bool polygonIsNormalized: true
    property real borderWidth: 0
    property color borderColor: color
    property bool debug: false
    property real xOffset: 0
    property real yOffset: 0

    property var bounds: roundedPolygon.calculateBounds()
    implicitWidth: bounds[2] - bounds[0]
    implicitHeight: bounds[3] - bounds[1]

    onRoundedPolygonChanged: requestPaint()
    onColorChanged: requestPaint()
    onBorderWidthChanged: requestPaint()
    onBorderColorChanged: requestPaint()
    onDebugChanged: requestPaint()
    onXOffsetChanged: requestPaint()
    onYOffsetChanged: requestPaint()

    onPaint: {
        var ctx = getContext("2d");
        ctx.fillStyle = root.color;
        ctx.clearRect(0, 0, width, height);
        const cubics = root.roundedPolygon?.cubics;
        if (!cubics || cubics.length === 0)
            return;
        const size = Math.min(root.width, root.height);

        ctx.save();
        if (root.polygonIsNormalized)
            ctx.scale(size, size);
        ctx.translate(root.xOffset, root.yOffset);

        ctx.beginPath();
        ctx.moveTo(cubics[0].anchor0X, cubics[0].anchor0Y);
        for (const cubic of cubics) {
            ctx.bezierCurveTo(cubic.control0X, cubic.control0Y, cubic.control1X, cubic.control1Y, cubic.anchor1X, cubic.anchor1Y);
        }
        ctx.closePath();
        ctx.fill();

        if (root.borderWidth > 0) {
            ctx.strokeStyle = root.borderColor;
            ctx.lineWidth = root.borderWidth;
            ctx.stroke();
        }

        if (root.debug) {
            const points = [];
            for (let i = 0; i < cubics.length; ++i) {
                const c = cubics[i];
                if (i === 0)
                    points.push({ x: c.anchor0X, y: c.anchor0Y });
                points.push({ x: c.anchor1X, y: c.anchor1Y });
            }
            ctx.fillStyle = "red";
            for (const p of points) {
                ctx.beginPath();
                ctx.arc(p.x, p.y, 2, 0, Math.PI * 2);
                ctx.fill();
            }
        }

        ctx.restore();
    }
}
